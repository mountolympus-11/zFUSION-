ITBSTSRV TITLE 'TABLE SERVICES - LOCAL STORAGE MANAGEMENT'              00010000
**<DOC-ON>*****************************************************         00020001
*                                                                       00030000
* ROUTINE: ITBSTSRV - LOCAL STORAGE MANAGEMENT                          00040000
*                                                                       00050000
* DESCRIPTION:                                                          00060000
*                                                                       00070000
*    THIS ROUTINE SATISFIES STORAGE 'GET' AND 'FREE' REQUESTS           00080000
*    DIRECTED AGAINST A STRUCTURE CONSISTING OF A HEADER AND AN         00090000
*    ASSOCIATED LIST OF FREE STORAGE BLOCKS.  EACH FREE STORAGE         00100000
*    BLOCK BEGINS WITH A FREE BLOCK DESCRIPTOR.                         00110000
*                                                                       00120000
*    THE FREE BLOCK DESCRIPTOR CONSISTS OF A FORWARD OFFLINK TO         00130000
*    THE NEXT FREE BLOCK AND A FULLWORD LENGTH.  THE HEADER HAS         00140000
*    THE SAME FORMAT AS A FREE BLOCK NODE, EXCEPT THAT THE SIZE         00150000
*    FIELD SUMMARIZES THE AMOUNT OF STORAGE AVAILABLE IN THE            00160000
*    ENTIRE FREE LIST.                                                  00170000
*                                                                       00180000
*    ALL STORAGE FUNCTIONS OPERATE IN DOUBLEWORD ALIGNED UNITS          00190000
*    OF EIGHT BYTES.  STORAGE REQUESTS NOT PROPERLY ALIGNED             00200000
*    ARE ROUNDED UP TO DOUBLEWORDS.  SUCCESSFUL 'GET' REQUESTS          00210000
*    RETURN THE AMOUNT OF STORAGE ACTUALLY ALLOCATED IN R0.             00220000
*                                                                       00230000
*    AN 'INIT' FUNCTION ALLOWS THE CALLER TO ASSIGN A BLOCK OF          00240000
*    STORAGE TO THE LOCAL STORAGE MANAGER.  THE BLOCK PROVIDED          00250000
*    IS ALIGNED TO A DOUBLEWORD BOUNDARY AND THE LENGTH IS              00260000
*    ADJUSTED TO DISCARD SLACK BYTES.                                   00270000
*                                                                       00280000
* NOTES:                                                                00290000
*                                                                       00300000
*    A MEMORY BASE PROVIDED IN ALL REQUEST FORMATS MAY BE USED          00310000
*    TO SUPPORT A RELOCATABLE MEMORY, WHERE EACH LINK CONSISTS          00320000
*    OF AN OFFSET FROM THE BASE VALUE. REGARDLESS OF WHETHER            00330000
*    NON-ZERO BASE VALUES ARE PROVIDED, THE STORAGE ADDRESSES           00340000
*    PROVIDED FOR INIT AND FREE, AS WELL AS THE ADDRESS                 00350000
*    RETURNED BY GET, ARE TRUE 31-BIT STORAGE ADDRESSES.                00360000
*                                                                       00370000
*                                                                       00380000
* ON ENTRY:                                                             00390000
*                                                                       00400000
*    R1  POINTS TO A PARAMETER LIST DESCRIBED BY THE $STG               00410000
*        MAPPING MACRO                                                  00420000
*                                                                       00430000
*                                                                       00440000
* ON EXIT:                                                              00450000
*                                                                       00460000
*    R15 CONTAINS ONE OF THE FOLLOWING RETURN CODES                     00470000
*                                                                       00480000
*         0 - FUNCTION COMPLETED                                        00490000
*         8 - ERROR ENCOUNTERED, FUNCTION NOT COMPLETED                 00500000
*                                                                       00510000
**<DOC-OFF>****************************************************         00520001
                                                                        00530001
         EJECT                                                          00540001
         $STG  TYPE=DSECT                                               00550001
                                                                        00560001
         EJECT                                                          00570001
         EQUS  LINKAGE=VCON                                             00580001
                                                                        00590001
         EJECT                                                          00600001
ITBSTSRV CSECT ,                                                        00610000
         BAKR  R14,0              SAVE CALLERS REGS                     00620000
         LAE   R12,0(R15,0)       ESTABLISH ADDRESSABILITY              00630000
         USING ITBSTSRV,R12       IN PRIMARY SPACE                      00640000
                                                                        00650000
         LAE   R7,0(0,R1)         FETCH PLIST ADDRESS                   00660000
         USING $STG,R7            PLIST RESIDENCE IS ARBITRARY          00670000
                                                                        00680000
         L     R11,$STGHDR        STORAGE MANAGEMENT HEADER             00690000
         LAM   AR11,AR11,$STGHLET IDENTIFY TARGET SPACE                 00700000
         USING $STENTRY,R11       SPACE WINDOW                          00710000
                                                                        00720000
         L     R4,$STESIZE        ??? DEBUG ???                         00730000
         N     R4,=X'FF000000'    ??? DEBUG ???                         00740000
         BZ    *+8                ??? DEBUG ???                         00750000
         EX    R0,*               ??? DEBUG ???                         00760000
                                                                        00770000
*--------------------------------------------------------------         00780000
*   NORMALIZE REQ - ALL REQUESTS AS MULTIPLES OF EIGHT BYTES            00790000
*--------------------------------------------------------------         00800000
         L     R4,$STGLEN         LENGTH OF STORAGE REQUESTED           00810000
         LA    R4,7(,R4)          ROUND UP TO DWORD BOUNDARY            00820000
         SRL   R4,3               DROP SLACK                            00830000
         SLL   R4,3               RESTORE                               00840000
                                                                        00850000
         L     R2,$STGREQ         REQUEST CODE                          00860000
         B     *+4(R2)            GOTO DEPENDING ON                     00870000
         B     INIT               INTIALIZE                             00880000
         B     GET                GET STORAGE                           00890000
         B     FREE               FREE STORAGE                          00900000
                                                                        00910000
         EJECT                                                          00920001
***************************************************************         00930000
*                                                                       00940000
* INIT - POST ALLOCATED STORAGE IN MANAGEMENT HDR                       00950000
*                                                                       00960000
***************************************************************         00970000
INIT     DS    0H                                                       00980000
         L     R2,$STGADDR        STORAGE EXTENT ORIGIN                 00990000
         LA    R3,7(,R2)          ROUND UP TO DWORD BOUNDARY            01000000
         SRL   R3,3               DROP SLACK                            01010000
         SLL   R3,3               RESTORE                               01020000
         ST    R3,$STGADDR        STORAGE ADDRESS                       01030000
         SR    R3,R2              NUMBER OF BYTES DISCARDED             01040000
                                                                        01050000
         L     R4,$STGLEN         STORAGE LENGTH                        01060000
         SR    R4,R3              ACCOUNT FOR WASTE                     01070000
         SRA   R4,3               ROUND DOWN TO DWORD MULTIPLE          01080000
         SLA   R4,3               RESTORE                               01090000
         ST    R4,$STGLEN         STORAGE LENGTH                        01100000
         BP    FREE               MERGE BLOCK INTO EXISTING FREE LIST   01110000
                                                                        01120000
*--------------------------------------------------------------         01130000
*   MAY REJECT THE STORAGE AREA PROVIDED                                01140000
*--------------------------------------------------------------         01150000
         LA    R15,RC08           STORAGE EXTENT ERROR                  01160000
         LAE   R1,0(0,0)          UNDEFINED                             01170000
         B     EXIT                                                     01180000
                                                                        01190000
         EJECT                                                          01200001
***************************************************************         01210000
*                                                                       01220000
* GET  - ACQUIRE AR QUALIFIED STORAGE BLOCK                             01230000
*                                                                       01240000
***************************************************************         01250000
GET      DS    0H                                                       01260000
         CL    R4,$STESIZE        CHANCE OF SATISFYING REQUEST          01270000
         BH    GET_ERR            NO WAY                                01280000
                                                                        01290000
P        USING $STENTRY,R11       "PRIOR" STORAGE BLOCK ENTRY           01300000
C        USING $STENTRY,R10       "CURRENT" STORAGE BLOCK ENTRY         01310000
                                                                        01320000
         LAE   R10,P.$STENTRY     START WITH CURRENT = PRIOR            01330000
                                                                        01340000
*--------------------------------------------------------------         01350000
*   LOCATE SUITABLE STORAGE BLOCK - FIRST FIT                           01360000
*--------------------------------------------------------------         01370000
GET_SCAN DS    0H                                                       01380000
         ICM   R10,15,P.$STENEXT  ADVANCE TO NEXT BLOCK                 01390000
         BZ    GET_ERR            NO SUITABLE EXTENT FOUND              01400000
         A     R10,$STGBASE       RELOC FROM MEMORY ORIGIN              01410000
         CL    R4,C.$STESIZE      REQUEST SIZE VS CURRENT.SIZE          01420000
         BL    GET_PART           CURRENT EXCEEDS REQUEST               01430000
         BE    GET_ALL            CURRENT EXACTLY SATISFIES REQUEST     01440000
         LR    R11,R10            ELSE PRIOR <== CURRENT                01450000
         B     GET_SCAN           AND TRY AGAIN                         01460000
                                                                        01470000
GET_PART DS    0H                 PARTIAL CONSUMPTION OF CURRENT        01480000
         L     R5,C.$STESIZE      SIZE DESCRIBED BY CURRENT             01490000
         SR    R5,R4              SPACE LEFT AFTER ALLOCATION           01500000
         ST    R5,C.$STESIZE      UPDATE CURRENT ACCORDINGLY            01510000
         LAE   R1,C.$STENTRY(R5)  RETURN ALLOCATED STORAGE PTR          01520000
         LR    R0,R4              RETURN TRUE LENGTH                    01530000
         B     GET_RET            CLEAR AND EXIT                        01540000
                                                                        01550000
GET_ALL  DS    0H                 EXTENT CONSUMES ENTIRE FREE BLOCK     01560000
         MVC   P.$STENEXT,C.$STENEXT                                    01570000
         LAE   R1,C.$STENTRY      REMOVE FREE NODE                      01580000
         LR    R0,R4              RETURN TRUE LENGTH                    01590000
         DROP  P,C                PRIOR AND CURRENT BLOCKS              01600000
                                                                        01610000
*--------------------------------------------------------------         01620000
*   CLEAR ALLOCATED STORAGE  R1 - ORIGIN, R0 - LENGTH                   01630000
*--------------------------------------------------------------         01640000
GET_RET  DS    0H                                                       01650000
         L     R11,$STGHDR        STORAGE MANAGEMENT HDR                01660000
         L     R2,$STESIZE        TOTAL MANAGED FREE SPACE SIZE         01670000
         SR    R2,R0              ACCOUNT FOR THIS ALLOCATION           01680000
         ST    R2,$STESIZE        SAVE UPDATED AMOUNT                   01690000
                                                                        01700000
         LAE   R6,0(,R1)          AR QUALIFIED ALLOCATED STORAGE ADDR   01710000
         LR    R7,R0              LENGTH OF STORAGE                     01720000
         SR    R15,R15            PREPARE FOR CLEAR                     01730000
         MVCL  R6,R14             CLEAR                                 01740000
         LA    R15,RC00           STORAGE SUCCESSFULLY ACQUIRED         01750000
         B     EXIT               DONE                                  01760000
                                                                        01770000
*--------------------------------------------------------------         01780000
*   ERROR: STORAGE NOT AVAILABLE                                        01790000
*--------------------------------------------------------------         01800000
GET_ERR  DS    0H                                                       01810000
         LA    R15,RC08           INDICATE ERROR                        01820000
         LAE   R1,0(0,0)          UNDEFINED                             01830000
         B     EXIT                                                     01840000
                                                                        01850000
         EJECT                                                          01860001
***************************************************************         01870000
*                                                                       01880000
* FREE - RELEASE STORAGE BLOCK                                          01890000
*                                                                       01900000
***************************************************************         01910000
FREE     DS    0H                                                       01920000
P        USING $STENTRY,R11       "PRIOR" STORAGE BLOCK ENTRY           01930000
C        USING $STENTRY,R10       "CURRENT" STORAGE BLOCK ENTRY         01940000
                                                                        01950000
         LAE   R10,P.$STENTRY     START WITH CURRENT = PRIOR            01960000
         L     R5,$STGADDR        ADDR OF STORAGE BLOCK                 01970000
                                                                        01980000
         CR    R11,R5             ??? DEBUG ???                         01990000
         BL    *+8                ??? DEBUG ???                         02000000
         EX    R0,*               ??? DEBUG ???                         02010000
                                                                        02020000
*--------------------------------------------------------------         02030000
*   LOCATE FREE CHAIN INSERTION POSITION                                02040000
*--------------------------------------------------------------         02050000
FREESCAN DS    0H                                                       02060000
         ICM   R10,15,P.$STENEXT  ADVANCE TO NEXT BLOCK                 02070000
         BZ    FREEISRT           NEW BLOCK ADDED TO END OF CHAIN       02080000
         A     R10,$STGBASE       RELOC FROM MEMORY ORIGIN              02090000
         CLR   R5,R10             ADD BLOCK BEFORE CURRENT?             02100000
         BL    FREEISRT           INSERT NOW IF SO                      02110000
         LR    R11,R10            ELSE PRIOR <== CURRENT                02120000
         B     FREESCAN           AND TRY AGAIN                         02130000
                                                                        02140000
FREEISRT DS    0H                 ATTACH FREE BLOCK TO PRIOR            02150000
         C     R11,$STGHDR        POSITIONED AT STORAGE HEADER?         02160000
         BE    FREENEW            CANT APPEND TO STORAGE HEADER         02170000
         LR    R3,R11             PRIOR NODE BASE                       02180000
         A     R3,P.$STESIZE      PLUS SIZE OF PRIOR                    02190000
         CLR   R3,R5              DOES PRIOR ABUT THE NEW STORAGE?      02200000
         BNE   FREENEW            NEW NODE REQUIRED IF NOT              02210000
         LR    R0,R4              MODIFIABLE COPY OF STORAGE LENGTH     02220000
         A     R0,P.$STESIZE      COMBINE STORAGE EXTENTS               02230000
         ST    R0,P.$STESIZE      SAVE UPDATED LENGTH                   02240000
         B     FREECOAL           NOW COALESCE WITH CURRENT             02250000
                                                                        02260000
FREENEW  DS    0H                 INSERT NEW FREE BLOCK DESCRIPTOR      02270000
NEW      USING $STENTRY,R5        DECLARE EXISTENCE OF NEW NODE         02280000
         CPYA  AR5,AR11           OPEN WINDOW TO STORAGE SPACE          02290000
         LR    R0,R5              COPY PTR TO NEW NODE                  02300000
         S     R0,$STGBASE        CONVERT TO OFFLINK                    02310000
         ST    R0,P.$STENEXT      SET PRIOR --> NEW                     02320000
         ST    R4,NEW.$STESIZE    POST STORAGE SIZE                     02330000
         ST    R10,NEW.$STENEXT   ASSUME ENTRY AT END OF LIST           02340000
                                                                        02350000
         LTR   R10,R10            END OF CHAIN?                         02360000
         BZ    FREEFIN            DONE IF SO                            02370000
         LR    R0,R10             MODIFIABLE COPY OF CURR NODE PTR      02380000
         S     R0,$STGBASE        ELSE CONVERT TO OFFLINK               02390000
         ST    R0,NEW.$STENEXT    AND RESET NEW --> CURRENT             02400000
                                                                        02410000
FREECOAL DS    0H                 COALESCE PRIOR/NEW NODE WITH CURRENT  02420000
         LR    R11,R5             SET PRIOR = NEW                       02430000
         LR    R3,R11             PRIOR NODE ORIGIN                     02440000
         A     R3,P.$STESIZE      PLUS SIZE OF PRIOR                    02450000
         CLR   R3,R10             DO EXTENTS ABUT?                      02460000
         BNE   FREEFIN            DONE IF NOT                           02470000
         MVC   P.$STENEXT,C.$STENEXT                                    02480000
         L     R2,P.$STESIZE      LENGTH OF PRIOR                       02490000
         A     R2,C.$STESIZE      PLUS LENGTH OF CURRENT                02500000
         ST    R2,P.$STESIZE      YIELDS TOTAL LENGTH OF FREE BLOCK     02510000
         DROP  P,C,NEW            PRIOR, CURRENT AND NEW                02520000
                                                                        02530000
FREEFIN  DS    0H                                                       02540000
         L     R11,$STGHDR        STORAGE MANAGEMENT HDR                02550000
         A     R4,$STESIZE        TOTAL MANAGED FREE SPACE SIZE         02560000
         ST    R4,$STESIZE        SAVE UPDATED AMOUNT                   02570000
                                                                        02580000
         LA    R15,RC00           NORMAL COMPLETION                     02590000
         LAE   R1,0(0,0)          UNDEFINED                             02600000
         B     EXIT                                                     02610000
                                                                        02620000
         EJECT                                                          02630001
***************************************************************         02640000
*                                                                       02650000
* RETURN COMPLETION STATUS TO REQUESTOR                                 02660000
*                                                                       02670000
***************************************************************         02680000
EXIT     NOP   0                                                        02690000
         L     R14,$STESIZE       ??? DEBUG ???                         02700000
         N     R14,=X'FF000000'   ??? DEBUG ???                         02710000
         BZ    *+8                ??? DEBUG ???                         02720000
         EX    R0,*               ??? DEBUG ???                         02730000
                                                                        02740000
         PR    ,                                                        02750000
                                                                        02760000
*--------------------------------------------------------------         02770000
* LOCAL READ/ONLY DATA AREAS, ETC.                                      02780000
*--------------------------------------------------------------         02790000
         LTORG ,                                                        02800000
         END   ,                                                        02810000
